'MacroName:Settings
'MacroDescription:Configure Settings for the macro
'Macro written by: Thomas Ventimiglia, Princeton University East Asian Library 
'Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the License.
'You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0
'Results are derived from the ScriptShifter utility (https://bibframe.org/scriptshifter/)
'Macro Created: 17 April 2025
'Macro last modified: 08 January 2026
'Macro version: 0.3.3

Declare Function ReplaceAll(sStr As String, sFind As String, sReplace as String) As String
Declare Function DialogFunc%(DlgItem$, Action%, SuppValue&)
Declare Function Row(iPos As Integer) As Integer
Declare Function Column(iPos As Integer) As Integer
Declare Function ControlWidth(iPos As Integer) As Integer
Declare Function ControlHeight(iPos As Integer) As Integer
Declare Sub GetLanguageList()
Declare Function GetJSONField(sJSON As String, sField As String) As String
Declare Sub AddExclusion(sSubfield as String)
Declare Sub DeleteExclusion(sSubfield as String)
Declare Sub SetExclusions(sExclusionList As String)

Global Const sVersion = "v0.3.3"
Global Const RowHeight = 20
Global Const ColumnWidth = 50   
Global Const MarginSize = 10
Global Const sSSURL = "https://bibframe.org/scriptshifter/"
Global Const sLangEndpoint = "languages"
Global Const sSettingsFilename = "ScriptShifter.Settings.json"
Global Const sDefaultJSON = "{""lang"":""asian_cyrillic"",""autoselect"":""true"",""capitalize"":""first"",""t_dir"":""auto""," & _
   """exclude"":['1xxe','7xxe','856u','xxxi'],""options"":{}}"

Global sLangCode
Global sLangLabel
Global sLangMARC
Global aLangTable$()
Global aLangList$()
Global aExcludeSubfields$()
Global sCfgLang
Global sCfgCapitalization
Global sCfgDirection
Global sCfgAutoSelect
Global CS As Object
Global oHTTP As Object
Global bSSerror

Sub Main
   On Error Resume Next
   Set CS = GetObject( , "Connex.Client" )
   On Error GoTo 0
   If CS Is Nothing Then Set CS = CreateObject( "Connex.Client" )
   
   Set oHTTP = CreateObject("WinHttp.WinHttpRequest.5.1")

   'Open settings file, or create it if it does not exist
   ChDir Environ("AppData") & "\OCLC\Connex\Macros"
   On Error GoTo MakeFile
   Open sSettingsFilename For Input As #1
ReadJSON:
   sJSON = ""
   While Not EOF(1)
      sChar = Input$(1,#1)
      sJSON = sJSON & sChar
   Wend
   If InStr(1,sJSON,"{") = 0 Then
      GoTo MakeFile
   End If
   'Load defaults form settings file
   sCfgLang = GetJSONField(sJSON,"lang")
   sCfgCapitalization = GetJSONField(sJSON, "capitalize")
   sCfgDirection = GetJSONField(sJSON, "t_dir")
   sCfgAutoSelect = GetJSONField(sJSON, "autoselect")
   sCfgExclusions = GetJSONField(sJSON, "exclude")
   Close #1
   Goto LoadData
MakeFile:
   Close #1
   Open sSettingsFilename For Output As #1
   Print #1, sDefaultJSON
   Close #1
   Open sSettingsFilename For Input As #1
   GoTo ReadJSON


LoadData:   
   sLangMARC = ""
   bSuccess = CS.GetFixedField("Lang",sLangMARC)

   bSSerror = False
   Call GetLanguageList
   If(bSSerror) Then
      Exit Sub
   End If

   sLangCode = ""
   sLangLabel = ""
      
   'Select SS language based on config file, or Lang fixed field if auto-selection is enabled
   'If SS defines multiple languages for a MARC language code, select the shortest 
   '(Assumes this is a reasonable "default", but has exceptions for Chinese and Korean)
   If sCfgAutoSelect = "false" Then
      For i = 0 to UBound(aLangTable$)
         If sCfgLang = aLangTable$(i,1) Then
            sLangCode = aLangTable$(i,1)
            sLangLabel = aLangTable$(i,0)
         End If   
      Next i 
   Else
      sLangLength = 999
      For i = 0 to UBound(aLangTable$)
         If (InStr(1,aLangTable$(i,2),sLangMARC) > 0) And _ 
            (Len(aLangTable$(i,2)) < sLangLength) And _ 
            (aLangTable$(i,1) <> "korean_names") And _ 
            (aLangTable$(i,1) <> "chiense") Then
            
            sLangLength = Len(aLangTable$(i,2))
            sLangCode = aLangTable$(i,1)
            sLangLabel = aLangTable$(i,0)
         End If   
      Next i   
   End if
   If sLangCode = "" Then
      sLangCode = aLangTable$(0,1)
      sLangLabel = aLangTable$(0,0)
   End if

   ReDim aExcludeSubfields$(0)
   SetExclusions(sCfgExclusions)   
   
   'Main dialog definition.  Rather than give exact cooridnates for each control, a simplified 'grid' model is used
   'based on 'cell' sizes defined by global variables, and functions that convert them to coordinates.  This allows
   'one to specify e.g. "place this button in column 2, row 3" 
      
   Begin Dialog SSDialog 10,10,Column(6), Row(9),"ScriptShifter Settings",.DialogFunc
      Text Column(1), Row(1), ControlWidth(1), ControlHeight(1), "Language:"
      DropListBox Column(2), Row(1), ControlWidth(2),ControlHeight(10),aLangList$(),.LangBox
      Text Column(4),Row(1),ControlWidth(2),ControlHeight(1),"",.Directionality
         
      CheckBox Column(1),Row(2),ControlWidth(4),ControlHeight(1),"Auto-select language based on 'Lang' fixed field",.AutoSelectLang
      
      Text Column(1),Row(3),ControlWidth(1),ControlHeight(1), "Direction:"
      OptionGroup .Direction
      OptionButton Column(2),Row(3),ControlWidth(1),ControlHeight(1),"Auto-detect"
      OptionButton Column(3),Row(3),ControlWidth(1)*1.5,ControlHeight(1),"Script-to-Roman"
      OptionButton Column(4)*1.1,Row(3),ControlWidth(1)*1.5,ControlHeight(1),"Roman-to-Script"
      
      Text Column(1), Row(4),ControlWidth(1),ControlHeight(1), "Capitalization:"
      OptionGroup .Capitalization
      OptionButton Column(2),Row(4),ControlWidth(1),ControlHeight(1),"No change"
      OptionButton Column(3),Row(4),ControlWidth(1),ControlHeight(1),"First word"
      OptionButton Column(4),Row(4),ControlWidth(1),ControlHeight(1),"All words"

      Text Column(1), Row(5),ControlWidth(2),ControlHeight(1), "Exclude Subfields:"
      TextBox Column(2), Row(5), ControlWidth(1), ControlHeight(1),.ExcludeText
      Button Column(3), Row(5), ControlWidth(1), ControlHeight(1),"Add"
      Button Column(3), Row(6), ControlWidth(1), ControlHeight(1),"Delete"
      ListBox Column(4),Row(5),ControlWidth(1),ControlHeight(3),aExcludeSubfields$(),.ExcludeBox
      
      OKButton  Column(1), Row(8), ControlWidth(1), ControlHeight(1)
      CancelButton  Column(2), Row(8), ControlWidth(1), ControlHeight(1)
      Text Column(5),Row(8),ControlWidth(1),ControlHeight(1),sVersion
      
   End Dialog
   Dim dlg as SSDialog    
On Error Resume Next
   bResult = Dialog(dlg)
   
   If bResult = 0 Then 'Cancel Button
      Exit Sub
   End If
   
   'OK button - write selections in dialog to settings file on disk
   sSelectedLang = aLangTable$(dlg.LangBox,1)
   sDirection = "auto"
   If dlg.Direction = 1 Then
      sDirection = "s2r"
   ElseIf dlg.Direction = 2 Then
      sDirection = "r2s"
   End If
   sCapitalization = "no_change"
   If dlg.Capitalization = 1 Then
      sCapitalization = "first"
   ElseIf dlg.Capitalization = 2 Then
      sCapitalization = "all"
   End If
   sAutoSelect = "false"
   If dlg.AutoSelectLang = 1 Then
      sAutoSelect = "true"
   End if 

   sExclusionList = "["
   For i = 0 to UBound(aExcludeSubfields)
      If aExcludeSubfields(i) <> "" Then  
         If i > 0 Then
            sExclusionList = sExclusionList  & ","
         End If
         sExclusionList = sExclusionList & "'" & aExcludeSubfields(i) & "'" 
      End If
   Next i
   sExclusionList = sExclusionList & "]"

   sJSON = "{""lang"":""" & sSelectedLang & """,""autoselect"":""" & sAutoSelect & """,""capitalize"":""" & _ 
      sCapitalization & """,""t_dir"":""" & sDirection & """,""exclude"":" & sExclusionList & ",""options"":{}}"
   'MsgBox sJSON
   Open sSettingsFilename For Output As #1
   Print #1, sJSON
   Close #1
End Sub

'*************************************
'Replace all instances of a substring
'*************************************

Function ReplaceAll(sStr As String, sFind As String, sReplace As String) As String
   place = 1
   If InStr(place, sStr, sFind) Then
      Do While InStr(place, sStr, sFind)
          place = InStr(place, sStr, sFind)
          sStr = Left(sStr, place - 1) & sReplace & Mid(sStr, place + Len(sFind))
          place = place + Len(sReplace)
      Loop
   End If
   ReplaceAll = sStr
End Function

'***************************************************************************************
'Get a current version of the SS language list and populate the in-memory language table
'***************************************************************************************

Sub GetLanguageList   
   sURL = sSSURL & sLangEndpoint
  
   oHTTP.Open "GET", sURL, False
   oHTTP.SetRequestHeader "Content-Type", "application/json"
   oHTTP.Send
   
   If (oHTTP.Status <> "200") Then
      MsgBox "Error connecting to the ScriptShifter service.  Please try again later."
      bSSerror = True
      Exit Sub
   End If
   
   sResponse = oHTTP.ResponseText
   
   iNextPos = InStr(1,sResponse,"},")
   iCount = 0
   While iNextPos > 0 
      iCount = iCount+1
      iNextPos = InStr(iNextPos+1,sResponse,"},")
   Wend
   
   ReDim aLangList$(iCount)
   ReDim aLangTable$(iCount,4)
   
   iCount = 0
   Do 
      sCurrentLang = sResponse
      If iNextPos > 0 Then
         sCurrentLang = Left(sResponse,iNextPos)
         sResponse = Mid(sResponse,iNextPos+1)
      End If
      sCurrentLang = Mid(sCurrentLang, InStr(1,sCurrentLang,Chr(34))+1)
      sDetails = Mid(sCurrentLang, InStr(1, sCurrentLang, "{"))
      sCurrentLang = Left(sCurrentLang, InStr(1,sCurrentLang, Chr(34))-1)
      
      sLabel = GetJSONField(sDetails,"label")
      sMARC = GetJSONField(sDetails,"marc_code")      
      sStoR = GetJSONField(sDetails,"has_s2r")     
      sRtoS = GetJSONField(sDetails,"has_r2s")
             
      sDirectionality = ""
      If sStoR = "true" And sRtoS = "false" Then
         sDirectionality = "Script-to-roman only"
      ElseIf sStoR = "false" And sRtoS = "true" Then
         sDirectionality = "Roman-to-Script only"
      End If
     
      aLangList(iCount) = sLabel
      aLangTable$(iCount,0) = sLabel
      aLangTable$(iCount,1) = sCurrentLang
      aLangTable$(iCount,2) = sMARC
      aLangTable$(iCount,3) = sDirectionality
      iCount = iCount + 1
      iNextPos = InStr(1,sResponse,"},")
   Loop While iNextPos > 0
End Sub

'**********************************
'Event handler for settings dialog
'**********************************

Function DialogFunc%(DlgItem$, Action%, SuppValue&)
   Select Case Action%
   Case 1 ' Dialog box initialization
      DlgText "LangBox",sLangLabel
      If DlgText$("LangBox") <> sLangLabel Then
         DlgValue "LangBox",DlgValue("LangBox")+1
      End If
      DlgText "Directionality",aLangTable$(DlgValue("LangBox"),3)
      If sCfgAutoSelect = "true" Then
         DlgValue "AutoSelectLang", 1
      End If
      If sCfgDirection = "r2s" Then
         DlgValue "Direction", 1
      End If
      If sCfgCapitalization = "first" Then
         DlgValue "Capitalization", 1
      ElseIf sCfgCapitalization = "all" Then
         DlgValue "Capitalization", 2
      End If
   Case 2 ' Value changing or button pressed
      Select Case DlgItem$
      Case "LangBox" 'update directionality label based on selected language
         sLangCode = aLangTable$(DlgValue(DlgItem$),1)
         DlgText "Directionality",aLangTable$(DlgValue("LangBox"),3)
      Case "Add" 'validate and add exclusion
         DialogFunc = True 
         sSubfield = DlgText("ExcludeText")
         If Not sSubfield Like "[0-9x][0-9x][0-9x][a-z]" Then
            MsgBox("'" & sSubfield & "' is not a valid subfield. Enter a 3-digit field code followed by subfield letter. " & _
               "Use 'x' as a wildcard. Valid examples include '300c', '1xxe', etc.")
            Exit Function
         End If
         AddExclusion(sSubfield)
         DlgListBoxArray "ExcludeBox", aExcludeSubfields
      Case "Delete" 'remove exclusion
         DialogFunc = True 
         If DlgValue("ExcludeBox") = -1 Then
            sSelected = ""
         Else
            sSelected = DlgText("ExcludeBox")
         End If
         If sSelected <> "" Then 
            DeleteExclusion(sSelected)
            DlgListBoxArray "ExcludeBox", aExcludeSubfields
         Else
            MsgBox("No value selected")
         End If
      End Select
   Case 3 ' TextBox or ComboBox text changed
   Case 4 ' Focus changed
   Case 5 ' Idle
   Case 6 ' Function key
      
   End Select
End Function

'**********************************************************************
'The following three functions update the in-memory list of exlcusions 
'**********************************************************************

Sub SetExclusions(sExclusionList As String)
   iNextComma = InStr(1,sExclusionList,",")   
   While iNextComma > 0  
      sNextExclusion = Left(sExclusionList,iNextComma-1)      
      AddExclusion(sNextExclusion)
      sExclusionList = Mid(sExclusionList,iNextComma+1)
      iNextComma = InStr(1,sExclusionList,",")  
   Wend
   If sExclusionList <> "" Then
      AddExclusion(sExclusionList)
   End If
End Sub

Sub AddExclusion(sSubfield as String) 
   sSubfield = ReplaceAll(sSubfield,"'","")
   Dim aCurrentExclusions()
   ReDim aCurrentExclusions(UBound(aExcludeSubfields$))
   For i = 0 to UBound(aExcludeSubfields$)
      If sSubfield = aExcludeSubfields(i) Then
         Exit Sub
      End If
      aCurrentExclusions(i) = aExcludeSubfields(i)
   Next i
   
   ReDim aExcludeSubfields(UBound(aExcludeSubfields$)+1)
   For i = 0 to UBound(aCurrentExclusions)
      aExcludeSubfields(i) = aCurrentExclusions(i)
   Next i   
   aExcludeSubfields(UBound(aExcludeSubfields$)-1) = sSubfield
End Sub

Sub DeleteExclusion(sSubfield as String)
   Dim aCurrentExclusions()
   ReDim aCurrentExclusions(UBound(aExcludeSubfields$)-1)
   j  = 0
   For i = 0 to UBound(aExcludeSubfields$)
      If sSubfield <> aExcludeSubfields(i) Then
         aCurrentExclusions(j) = aExcludeSubfields(i)
         j = j + 1
      End If
   Next i
   
   ReDim aExcludeSubfields(UBound(aExcludeSubfields$)-1)
   For i = 0 to UBound(aCurrentExclusions)
      aExcludeSubfields(i) = aCurrentExclusions(i)
   Next i   
End Sub

'*****************************************************************************
'Extracts the value corresponding to a JSON field name in the provided string
'*****************************************************************************

Function GetJSONField(sJSON As String, sField As String) As String
   iLoc = InStr(1,sJSON, Chr(34) & sField & Chr(34))
   sStr = sJSON
   If iLoc > 0 Then
      sStr = Mid(sStr,iLoc+Len(sField)+3)
      sFirstChar = Left(sStr,1)
      sStr = Mid(sStr,2)
      If sFirstChar = Chr(34) Then
         sStr = Left(sStr,InStr(1,sStr,Chr(34))-1)
      ElseIf sFirstChar = "[" Then
         sStr = Left(sStr,InStr(1,sStr,"]")-1)
      ElseIf InStr(1,sStr,",") > 0 Then
         sStr = sFirstChar & Left(sStr,InStr(1,sStr,",")-1)
      ElseIf InStr(1,sStr,"}") > 0 Then
         sStr = sFirstChar & Left(sStr,InStr(1,sStr,"}")-1)
      Else
         sStr = sFirstChar & sStr
      End If
      GetJSONField = sStr
   Else
      GetJSONField = ""
   End If
End Function

'**********************************************************************
'These functions return the position and size of various 'cells' in the 
'dialog 'grid' based on predefined global variables.  This simplifies
'the parameters used in the design of the main dialog 
'**********************************************************************

Function Row(iPos As Integer) As Integer
   Row = MarginSize*iPos + RowHeight*(iPos-1)
End Function

Function Column(iPos As Integer) As Integer
   Column = MarginSize*iPos + ColumnWidth*(iPos-1)
End Function

Function ControlWidth(iPos As Integer) As Integer
   ControlWidth = CInt((MarginSize*(iPos-1) + ColumnWidth*iPos)*1)
End Function

Function ControlHeight(iPos As Integer) As Integer
   ControlHeight = CInt((MarginSize*(iPos-1) + RowHeight*iPos)*0.8)
End Function
   
