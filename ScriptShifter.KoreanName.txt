'MacroName:KoreanName
'MacroDescription:Reconvert a Korean name field using ScriptShifter
'Macro written by: Thomas Ventimiglia, Princeton University East Asian Library 
'Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the License.
'You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0
'Results are derived from the ScriptShifter utility (https://bibframe.org/scriptshifter/)
'Macro Created: 17 April 2025
'Macro last modified: 08 January 2026
'Macro version: 0.3.3

Global Const sDefaultJSON = "{""lang"":""korean_names"",""capitalize"":""no_change"",""t_dir"":""s2r"",""options"":{}}"
Global Const sScriptShifterURL = "https://bibframe.org/scriptshifter/"
Global Const sTransEndpoint = "trans"
Global oHTTP as Object

Declare Function ReplaceAll(sStr As String, sFind As String, sReplace as String) As String
Declare Function ReplaceAllNoCase(sStr As String, sFind As String, sReplace as String) As String
Declare Function ConvertHTMLEntities(sStr As String) As String
Declare Function ConvertUnicodeEntities(sStr As String) As String
Declare Function GetUnicodeSubstring(sStr As String, iStart As Integer, iLength As Integer) As String
Declare Function QueryScriptShifter(sReqJSON As String) As String
Declare Function GetFieldByLine(CS As Object, iLineNo As Integer) As String

Sub Main
   Dim CS As Object
   On Error Resume Next
   Set CS  = GetObject(,"Connex.Client")
   On Error GoTo 0
   If CS  Is Nothing Then
      Set CS  = CreateObject("Connex.Client")
   End If
   sSelection = ""
   startSpace = ""
   endSpace = ""
   sTitle = "Convert Korean Names"

   Set oHTTP = CreateObject("WinHttp.WinHttpRequest.5.1")
   
   'Capture location and length of selected text
   CS.CopySelected
   iSelRow = CS.CursorRow
   iSelCol = CS.CursorColumn
   iSelLen = Len(Clipboard.GetText)

   If iSelLen = 0 Then
      MsgBox "No text selected",0,sTitle
      Exit Sub
   End If   

   sSelection = GetFieldByLine(CS,iSelRow)

   'Extract selected Unicode text using parameters captured earlier
   sSelection = GetUnicodeSubstring(sSelection,iSelCol,iSelLen)

   'If selection starts or ends with a space or period, strip it and add it back in later.
   If Left(sSelection,1) = " " Or Left(sSelection,1) = "."Then
      startSpace = Left(sSelection,1)
      sSelection = Mid(sSelection,2)
   End If
   If Right(sSelection,1) = " " Or Right(sSelection,1) = "." Then
      endSpace = Right(sSelection,1) 
      sSelection = Left(sSelection,Len(sSelection)-1)
   End If

   If Len(sSelection) = 0 Then
      MsgBox "No text selected",0,sTitle
      Exit Sub
   End If
   
   'Convert selected text using Korean names mode of SS
   sSelection = ReplaceAll(sSelection,Chr(34),"\" & Chr(34))
   sSelection = ConvertHTMLEntities(sSelection) 

   sReqJSON = ReplaceAll(sDefaultJSON, "{""lang","{""text"":""" & sSelection & """,""lang")
   sRomanName = QueryScriptShifter(sReqJSON)
   If sRomanName = "" Then
      Exit Sub
   End If

   sRomanName = ConvertUnicodeEntities(sRomanName)

   'Convert the same text in 'no names' mode in order to know what to replace
   sReqJSON = ReplaceAll(sReqJSON,"korean_names", "korean_nonames")
   sRomanNormal = QueryScriptShifter(sReqJSON)
   sRomanNormal = ConvertUnicodeEntities(sRomanNormal)

   If sRomanName = sRomanNormal or sRomanNormal = "" Then
      Exit Sub
   End If
   
   nLine = CS.CursorRow
   nNextLine = nLine + 1
   
   Dim sCharField As string
   bSuccess = CS.GetFieldLineUnicode(nLine, sCharField)
   
   CS.CursorRow = nNextLine
   sRomanField = GetFieldByLine(CS,nNextLine)

   'The parallel field must appear right above the Romanized field and have the same tag.      
   If sRomanField = "" Then
      MsgBox "No parallel field",0,sTitle
      Exit Sub
   End If
   If Left(sRomanField, 3) <> Left(sCharField, 3) Then
      MsgBox "No parallel field",0,sTitle
      Exit Sub
   End If

   'Replace roman text in orignal conversion with re-converted text, output to record
   sRomanField = ReplaceAllNoCase(sRomanField,sRomanNormal,sRomanName)
   CS.SetFieldLine nNextLine,sRomanField
   
End Sub

'******************************************************
'Query ScriptShifter using provided JSON request string
'******************************************************

Function QueryScriptShifter(sReqJSON As String) As String
   oHTTP.Open "POST", sScriptShifterURL & sTransEndpoint, False
   oHTTP.SetRequestHeader "Content-Type", "application/json"
   oHTTP.Send(sReqJSON)
   
   If (oHTTP.Status <> "200") Then
      MsgBox "Error connecting to the ScriptShifter service.  Please try again later."
      QueryScriptShifter = ""
      Exit Function
   End If
   
   sResponse = oHTTP.ResponseText
   sResponse = ReplaceAll(sResponse,"{""output"":""","")
   sResponse = Left(sResponse,InStr(1,sResponse,""",""warnings")-1)
   sResponse = ReplaceAll(sResponse, Chr(10),"")
   sResponse = ReplaceAll(sResponse, "\" & Chr(34), Chr(34))
      
   QueryScriptShifter = sResponse
End Function

'********************************************************************
'Extract substring with given start position and length, but treating 
'Unicode NCRs as single characters
'********************************************************************

Function GetUnicodeSubstring(sStr As String, iStart As Integer, iLength As Integer) As String
   iStrLen = Len(sStr)
   iActualPos = 1
   iActualLen = 0
   sSubstring = ""
   sChar = ""
   bInEntity = False
   For i = 1 to iStrLen
      If Mid(sStr,i,3) = "&#x" Then
         bInEntity = True
      ElseIf Mid(sStr,i,1) = ";" Then
         bInEntity = False
      End If
      If iActualPos >= iStart And iActualLen < iLength Then
         sSubstring = sSubstring & Mid(sStr,i,1)
         If Not bInEntity Then
            iActualLen = iActualLen + 1
         End If
      End If
      If Not bInEntity Then
         iActualPos = iActualPos + 1
      End If
   Next i
   GetUnicodeSubstring = sSubstring
End Function

'***************************************************************
'Convert Unicode NCRs to JSON entities (e.g. &#x1234; -> \u1234) 
'***************************************************************

Function ConvertHTMLEntities(sStr As String) As String
   iPos = InStr(1,sStr,"&#x")
   While iPos > 0
      sStr = Left(sStr,iPos-1) & "\u" & Mid(sStr,iPos+3,4) & Mid(sStr,iPos+8)   
      iPos = InStr(1,sStr,"&#x") 
   Wend
   ConvertHTMLEntities = sStr
End Function

'***************************************************************
'Convert JSON entities to Unicode NCRs (e.g. \u1234 -> &#x1234;) 
'***************************************************************

Function ConvertUnicodeEntities(sStr As String) As String
   iPos = InStr(1,sStr,"\u")
   While iPos > 0
      sStr = Left(sStr,iPos-1) & "&#x" & UCase(Mid(sStr,iPos+2,4)) & ";" & Mid(sStr,iPos+6)  
      iPos = InStr(1,sStr,"\u") 
   Wend
   ConvertUnicodeEntities = sStr
End Function

'*******************************************************
'Replace all instances of a substring (case-insensitive)
'*******************************************************

Function ReplaceAllNoCase(sStr As String, sFind As String, sReplace As String) As String
   place = 1
   If InStr(place, LCase(sStr), LCase(sFind)) Then
      Do While InStr(place, LCase(sStr), LCase(sFind))
          place = InStr(place, LCase(sStr), LCase(sFind))
          sStr = Left(sStr, place - 1) & sReplace & Mid(sStr, place + Len(sFind))
          place = place + Len(sReplace)
      Loop
   End If
   ReplaceAllNoCase = sStr
End Function


'*******************************************************
'Replace all instances of a substring (case-sensitive)
'*******************************************************

Function ReplaceAll(sStr As String, sFind As String, sReplace As String) As String
   place = 1
   If InStr(place, sStr, sFind) Then
      Do While InStr(place, sStr, sFind)
          place = InStr(place, sStr, sFind)
          sStr = Left(sStr, place - 1) & sReplace & Mid(sStr, place + Len(sFind))
          place = place + Len(sReplace)
      Loop
   End If
   ReplaceAll = sStr
End Function

'***************************************************************************
'Retrieve field at given line number.  Workaround for GetFieldLineUnicode bug
'in which Unicode characters are not consistently represented as NCRs
'***************************************************************************

Function GetFieldByLine(CS As Object, iLineNo As Integer) As String
   Dim sField As String

   'Insert BOM at end of string to force Unicode to appear as NCR
   CS.EndCell
   CS.InsertText("&#xFEFF;")
   bSuccess = CS.GetFieldLineUnicode(iLineNo,sField)
   CS.EndCell
   CS.SendKeys "",-1 
   CS.SendKeys "{BS}",-1
   If Not bSuccess Then
      sField = ""
   End If
   GetFieldByLine = sField   
End Function
