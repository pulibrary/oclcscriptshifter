'MacroName:Convert
'MacroDescription:Convert a field using the ScriptShifter utility
'Macro written by: Thomas Ventimiglia, Princeton University East Asian Library 
'Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the License.
'You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0
'Macro Created: 17 April 2025
'Macro last modified: 14 January 2026
'Macro version: 0.3.3

Declare Function ReplaceAll(sStr As String, sFind As String, sReplace as String) As String
Declare Function ValidateField(sStr As String) As String
Declare Function GetHexCode(sStr As String)
Declare Function ConvertHTMLEntities(sStr As String) As String
Declare Function ConvertUnicodeEntities(sStr As String) As String
Declare Function ConvertLatin1(sStr As String) As String
Declare Function GetJSONField(sJSON As String, sField As String) As String
Declare Function AdjustKorLangCode(sJSON As String, bIsName) As String
Declare Sub GetLanguageList(ByRef aLangTable$())
Declare Function IsRomanized(sString As String) As Integer
Declare Function AddExclusion(sSubfield as String)
Declare Sub SetExclusions(sExclusionList As String)
Declare Function IsExcluded(sSubfield As String) As Integer
Declare Function GetUnicodeSubstring(sStr As String, iStart As Integer, iLength As Integer) As String
Declare Function GetCurrentField(CS As Object) As String

Global Const sSettingsFilename = "ScriptShifter.Settings.json"
Global Const sScriptShifterURL = "https://bibframe.org/scriptshifter/"
Global Const sLangEndpoint = "languages"
Global Const sTransEndpoint = "trans"
Global aExcludeSubfields$()
Global oHTTP As Object

Sub Main
   Dim CS As Object
   On Error Resume Next
   Set CS = GetObject( , "Connex.Client" )
   On Error GoTo 0
   If CS Is Nothing Then Set CS = CreateObject( "Connex.Client" )

   Set oHTTP = CreateObject("WinHttp.WinHttpRequest.5.1")

   Clipboard.Clear

   'Capture selected text (to be excluded from conversion)
   CS.CopySelected
   iSelRow = CS.CursorRow
   iSelCol = CS.CursorColumn-5
   sExcludedText = Clipboard.GetText
   iSelLen = Len(sExcludedText)

   'Load Settings file, or launch settings macro if none exists
   ChDir Environ("AppData") & "\OCLC\Connex\Macros"
   On Error GoTo MakeFile
ReadJSON:
   sJSON = ""
   Open sSettingsFilename For Input As #1
   While Not EOF(1)
      sChar = Input$(1,#1)
      sJSON = sJSON & sChar
   Wend
   sCfgLang = GetJSONField(sJSON,"lang")
   sCfgCapitalization = GetJSONField(sJSON, "capitalize")
   sCfgDirection = GetJSONField(sJSON, "t_dir")
   sCfgAutoSelect = GetJSONField(sJSON, "autoselect")
   sCfgExclusions = GetJSONField(sJSON, "exclude")
   Close #1
   On Error Resume Next
   Goto Convert
MakeFile:
   CS.RunMacro("ScriptShifter!Settings")
   Goto ReadJSON:
Convert:

   ReDim aExcludeSubfields$(0)
   SetExclusions(sCfgExclusions)   

   sDelimiterChar = Chr(31)
   sDelimiterHTML = "&#x01C2;"
   
   'Uncontrol heading if needed (so it can be edited)
   bControlled = (CS.IsHeadingControlled(CS.CursorRow) <> 0)

   If bControlled Then
      CS.UncontrolHeading
   End If
      
   sField = GetCurrentField(CS)
         
   'Replace EOP character in indicator fields with period
   sField = ReplaceAll(sField, "&#x220E;",".")
  
   sOriginalField = sField

   'Convert subfield Delimiter to ASCII 1F
   sField = ReplaceAll(sField, sDelimiterHTML,sDelimiterChar)
   
   'Pull the tag name and indicators
   sConvertedFields = Left(sField, 5)
   sTag = Left(sConvertedFields, 3)
   'The text of the field
   sField = Mid(sField, 6)

   bExcludeSelection = False
   'Determine the start and end of the selection in the version of the field with Unicode NCRs
   If iSelLen > 0 Then
      bExcludeSelection = True
      sExcludedText = GetUnicodeSubstring(sField,iSelCol,iSelLen)
      iExcludeStart = Len(GetUnicodeSubstring(sField,1,iSelCol))
      iExcludeEnd = iExcludeStart + Len(sExcludedText)      
      sExcludedText = ReplaceAll(sExcludedText,sDelimiterChar,sDelimiterHTML)
   End If


   'Select SS language based on config-file (or Lang fixed field if auto-selection is enabled)
   sLangName = ""
   sLangCode = ""
   sDirectionality = ""
   If sCfgAutoSelect = "true" Then
      Dim aLangTable$()
      Call GetLanguageList(aLangTable$)
      sLangMARC = ""
      bSuccess = CS.GetFixedField("Lang",sLangMARC)
      sLangLength = 99
      For i = 0 to UBound(aLangTable)
         If (InStr(1,aLangTable(i,2),sLangMARC) > 0) And (Len(aLangTable(i,2)) < sLangLength) Then
            sLangLength = Len(aLangTable(i,2))
            sLangCode = aLangTable(i,1)
            sLangName = aLangTable(i,0)
            sDirectionality = aLangTable(i,3)
         End If   
      Next i   
   End if
   If sLangCode <> "" Then
      sJSON = ReplaceAll(sJSON,sCfgLang,sLangCode)
   End If

   bFieldHasNonRoman = IsRomanized(ConvertHTMLEntities(sField))

   bInExclusion = False

   iFieldPos = 1

   bSSError = False

   'Iterate through subfields
   Do
      iDelimPos = InStr(iFieldPos+1,sField,sDelimiterChar)
            
      If(bExcludeSelection And iFieldPos < iExcludeEnd And _ 
         (iDelimPos >= iExcludeStart Or iDelimPos = 0)) Then
         bInExclusion = True
         iDelimPos = iExcludeStart
      End If

      If iDelimPos > 0 Then
        sCurrentSubfield = Mid(sField,iFieldPos,iDelimPos-iFieldPos)
      Else
        sCurrentSubfield = Mid(sField,iFieldPos,Len(sField)-iFieldPos+1) 
      End If

      iFieldPos = iFieldPos + Len(sCurrentSubfield)
           
      sSubfieldTag = ""
      If Left(sCurrentSubfield,1) = sDelimiterChar Then
         sSubfieldTag = Mid(sCurrentSubfield,2,1)
         sCurrentSubfield = Mid(sCurrentSubfield,3)
      End If

      'Remove certain trailing spaces and punctuation from subfield before conversion
      '(to be appended to converted string after)
      sSuffix = ""
      If Len(sCurrentSubfield) > 0 Then
         sLastChar = Right(sCurrentSubfield,1) 
         Do While (Len(sCurrentSubfield) > 0) And _ 
               ((sLastChar = " ") Or (sLastChar = ":") Or (sLastChar = "/") Or _ 
               (sLastChar = ".") Or (Right(sCurrentSubfield,2) = " ;"))
            sSuffix = sLastChar & sSuffix
            sCurrentSubfield = Left(sCurrentSubfield,Len(sCurrentSubfield)-1)
            sLastChar = Right(sCurrentSubfield,1) 
         Loop
      End If

      'always skip numeric subfields      
      If (sSubfieldTag >= "0" And sSubfieldTag <= "9") Or IsExcluded(sTag & sSubfieldTag) Then
         sResponse = sCurrentSubfield
         GoTo AppendResponse            
      End If   
 
      sCurrentSubfield = ReplaceAll(sCurrentSubfield,Chr(34),"\" & Chr(34))
      sCurrentSubfield = ConvertHTMLEntities(sCurrentSubfield) 
     
      bIsName = False
      If (sTag = "100" Or sTag = "600" Or sTag = "700" or sTag = "800") And (sSubfieldTag = "a" Or sSubfieldTag = "") Then
            bIsName = True
      End If
      
      'Language-specific adjustments to input
      
      'Korean: Select correct language based on whether field is a 'name' field (100a, etc.)
      If InStr(1,sJSON,"korean") Then
         If bIsName = True Then
            sJSON = ReplaceAll(sJSON,"korean_nonames","korean_names")
         Else
            sJSON = ReplaceAll(sJSON,"korean_names","korean_nonames")
         End If
         sLangName = "Korean"
      End If
   
      'Chinese: Default to Chinese (numerals) setting if auto-selection on.  
      'If 'name' field, set marc_field optional param
      If InStr(1,sJSON,"chinese") Then
         If sCfgAutoSelect = "true" Then
            sJSON = ReplaceAll(sJSON, Chr(34) & "chinese" & Chr(34),Chr(34) & "chinese_numerals" & Chr(34))
         End If
         If bIsName Then
            sJSON = ReplaceAll(sJSON, Chr(34) & "options" & Chr(34) & ":{}",Chr(34) & "options" & _ 
               Chr(34) & ":{" & Chr(34) & "marc_field" & Chr(34) & ":" & Chr(34) & sTag & Chr(34) & "}")
         End If
         sLangName = "Chinese (Arabic Numerals)"
      End If
   
      'Hebrew: Default to 'modern' genre
      If (sLangCode = "hebrew") Then
         sJSON = ReplaceAll(sJSON, Chr(34) & "options" & Chr(34) & ":{}",Chr(34) & "options" & _ 
               Chr(34) & ":{" & Chr(34) & "genre" & Chr(34) & ":" & Chr(34) & "modern" & Chr(34) & "}")
      End If    
   
      Dim sReqJSON As String
      sReqJSON = sJSON
      sReqJSON = ReplaceAll(sReqJSON, "{""lang","{""text"":""" & sCurrentSubfield & """,""lang")

      'auto-detect roman vs non-roman
      If InStr(1,sReqJSON,Chr(34) & "t_dir" & Chr(34) & ":" & Chr(34) & "auto") > 0 Then 
         If bFieldHasNonRoman Then
            sReqJSON = ReplaceAll(sReqJSON,Chr(34) & "auto" & Chr(34),Chr(34) & "r2s" & Chr(34))
         Else
            sReqJSON = ReplaceAll(sReqJSON,Chr(34) & "auto" & Chr(34),Chr(34) & "s2r" & Chr(34))
         End If
      End If
      
      'Abort if directionality not supported

      sConvDirection = ""
      If InStr(1,sReqJSON,"r2s") > 0 Then
         sConvDirection = "r2s"
      Else
         sConvDirection = "s2r"
      End If
      
      If sDirectionality = "s2r" And sConvDirection = "r2s" Then
         MsgBox(sLangName & " only supports script-to-roman conversion.")
         Exit Sub
      End If
      If sDirectionality = "r2s" And sConvDirection = "s2r" Then
         MsgBox(sLangName & " only supports roman-to-script conversion.")
         Exit Sub
      End If

      
      'If s2r, Skip subfields that do not contain Unicode entities (i.e. likely already romanized)
      If sConvDirection = "s2r" And InStr(1,sCurrentSubfield,"\u") = 0 Then
         sResponse = sCurrentSubfield
         GoTo AppendResponse            
      End If

      If Len(sCurrentSubfield) = 0 Then
         sResponse = ""
         GoTo AppendResponse
      End If

      'Remove exclusions from Request JSON
      iExclusionPos = InStr(1,sReqJSON, Chr(34) & "exclude" & Chr(34))
      If iExclusionPos > 0 Then
         iExclusionEnd = InStr(iExclusionPos, sReqJSON, "],")
         sReqJSON = Left(sReqJSON,iExclusionPos-1) & Mid(sReqJSON, iExclusionEnd+2)
      End If

      oHTTP.Open "POST", sScriptShifterURL & sTransEndpoint, False
      oHTTP.SetRequestHeader "Content-Type", "application/json"
      
      oHTTP.Send(sReqJSON)
      
      If (oHTTP.Status <> "200" And oHTTP.Status <> "500") Then
         MsgBox "Error connecting to the ScriptShifter service.  Please try again later."
         Exit Sub
      End If

      If oHTTP.Status = "500" Then
         bSSError = True
         sResponse = sCurrentSubfield
         GoTo AppendResponse
      End If         

      sResponse = oHTTP.ResponseText
      sResponse = ReplaceAll(sResponse,"{""output"":""","")
      sResponse = Left(sResponse,InStr(1,sResponse,""",""warnings")-1)
      sResponse = ReplaceAll(sResponse, Chr(10),"")
      sResponse = ReplaceAll(sResponse, "\" & Chr(34), Chr(34))
   
      If Trim(LCase(sResponse)) = Trim(LCase(sCurrentSubfield)) Then
         sResponse = sCurrentSubfield
         GoTo AppendResponse
      End If   
   
      'Special name formatting for Korean
      If bIsName And InStr(1,sJSON,"korean_names") > 0 Then
        iSpacePos = InStr(1,sResponse," ")
        If iSpacePos > 0 Then
         sResponse = Left(sResponse,iSpacePos-1) & "," & Mid(sResponse,iSpacePos)
        End If
      End If

AppendResponse:   

      sResponse = ConvertUnicodeEntities(sResponse)
      
      'Greek (remove stress marks, convert "O" to "Ho"
      If InStr(1, sReqJSON, "lang"":""greek") > 0 And sConvDirection = "s2r" Then
         sResponse = ReplaceAll(sResponse, "O ","Ho ")
         sResponse = ReplaceAll(sResponse,"&#x0301;","")
      End If


      'Splice excluded text into response
      If sSubfieldTag <> "" Then
         sResponse = sDelimiterHTML & sSubfieldTag & " " & Trim(sResponse)
      End If
      sResponse = Trim(sResponse) & sSuffix
      sConvertedFields = sConvertedFields & sResponse
      If(bInExclusion) Then
         bInExclusion = False
         sConvertedFields = sConvertedFields & sExcludedText
         iFieldPos = iExcludeEnd
      End If

   Loop While iDelimPos > 0 And iFieldPos <= Len(sField)
         
   'Restore EOP characters to tag and indicator fields
   i = 5
   Do While(i > 0)
      If(Mid(sConvertedFields,i,1) = ".") Then
         sConvertedFields = Left(sConvertedFields,i-1) & "&#x220E;" & Mid(sConvertedFields,i+1)
      End If
      i = i - 1
   Loop

   sConvertedFields = ConvertLatin1(sConvertedFields)
  
   If sConvDirection = "s2r" Then
      bool = CS.AddFieldLine(CS.CursorRow + 1, sConvertedFields)
      CS.CursorRow = CS.CursorRow - 1
   Else
      bool = CS.AddFieldLine(CS.CursorRow, sConvertedFields)     
   End If

   CS.SendKeys "", -1
   CS.SendKeys "%(ekl)", -1

   If bControlled Then
      If sConvDirection = "r2s" Then
         CS.CursorRow = CS.CursorRow + 1
      End If
      CS.SendKeys "{F11}",-1
      If sConvDirection = "r2s" Then
         CS.CursorRow = CS.CursorRow - 1
      End If
   End If
  
   If bSSError Then
      MsgBox("Not all subfields were converted.  Please check the input field for errors.")
   End If  
End Sub

Function AdjustKorLangCode(sJSON As String, bIsName) As String
   If bIsName = True Then
      sJSON = ReplaceAll(sJSON,"korean_nonames","korean_names")
   Else
      sJSON = ReplaceAll(sJSON,"korean_names","korean_nonames")
   End If
   AdjustKorLangCode = sJSON
End Function 

Function ValidateField(sStr As String) As String
   nLen = Len(sStr)
   sResult = ""
   pos = 1
   For i = 6 to nLen
      If Mid(sStr,i,8) Like "&[#]x[0-9A-F][0-9A-F][0-9A-F][0-9A-F];" Then
         sNum = Mid(sStr,i,8)
         nNum = GetHexCode(sNum)
         i = i + 7
      Else
         nNum = Asc(Mid(sStr,i,1))
         sNum = Val(nNum)        
      End If
      If Not((nNum > 0 And nNum < &H00FF&) Or nNum = &H0110& Or nNum = &H0111& Or nNum = &H0131& Or nNum = &H0141& Or nNum = &H0142& Or nNum = &H0152& Or nNum = &H0153& Or nNum = &H0300& Or nNum = &H0301& Or nNum = &H0302& Or nNum = &H0303& Or nNum = &H0304& Or nNum = &H0306& Or nNum = &H0307& Or nNum = &H0308& Or nNum = &H0309& Or nNum = &H0310& Or nNum = &H0313& Or nNum = &H0315& Or nNum = &H0323& Or nNum = &H0324& Or nNum = &H0325& Or nNum = &H0326& Or nNum = &H0327& Or nNum = &H0328& Or nNum = &H0332& Or nNum = &H0333& Or nNum = &H2113& Or nNum = &H2117& Or nNum = &H00A1& Or nNum = &H00A3& Or nNum = &H00A9& Or nNum = &H00AE& Or nNum = &H00B0& Or nNum = &H00B1& Or nNum = &H00B7& Or nNum = &H00BF& Or nNum = &H00C6& Or nNum = &H00D8& Or nNum = &H00DE& Or nNum = &H00DF& Or nNum = &H00F0& Or nNum = &H00F8& Or nNum = &H00FE& Or nNum = &H01A0& Or nNum = &H01A1& Or nNum = &H01AF& Or nNum = &H01B0& Or nNum = &H02B9& Or nNum = &H02BA& Or nNum = &H02BB& Or nNum = &H02BC& Or nNum = &H030A& Or nNum = &H030B& Or nNum = &H030C& Or nNum = &H031C& Or nNum = &H032E& Or nNum = &H266D& Or nNum = &H266F& Or nNum = &HFE20& Or nNum = &HFE21& Or nNum = &HFE22& Or nNum = &HFE23& Or nNum = &H01C2&) Then
         sResult = sResult & "Invalid character in result at position " & pos
         sResult = sResult & ": x" & Hex(nNum) & chr(10)
      End If
      pos = pos + 1
   Next i
   ValidateField = sResult
End Function

Function ReplaceAll(sStr As String, sFind As String, sReplace As String) As String
   place = 1
   If InStr(place, sStr, sFind) Then
      Do While InStr(place, sStr, sFind)
          place = InStr(place, sStr, sFind)
          sStr = Left(sStr, place - 1) & sReplace & Mid(sStr, place + Len(sFind))
          place = place + Len(sReplace)
      Loop
   End If
   ReplaceAll = sStr
End Function

Function GetHexCode(sChar As String) as Long
   sHex = Mid(sChar, 4, 4)
   nHex = Val("&H" & sHex & "&")
   If nHex < 0 Then
      nHex = nHex + &HFFFF& + 1
   End If
   GetHexCode = nHex 
End Function

Function ConvertLatin1(sStr As String) As String
   For i = 128 to 255
      sStr = ReplaceAll(sStr, Chr(i), "&#x00" & Hex(i) & ";")
   Next i
   ConvertLatin1 = sStr
End Function

Function ConvertHTMLEntities(sStr As String) As String
   sConverted = sStr
   iPos = InStr(1,sConverted,"&#x")
   While iPos > 0
      sConverted = Left(sConverted,iPos-1) & "\u" & Mid(sConverted,iPos+3,4) & Mid(sConverted,iPos+8)   
      iPos = InStr(1,sConverted,"&#x") 
   Wend
   ConvertHTMLEntities = sConverted
End Function

Function ConvertUnicodeEntities(sStr As String) As String
   iPos = InStr(1,sStr,"\u")
   While iPos > 0
      sStr = Left(sStr,iPos-1) & "&#x" & UCase(Mid(sStr,iPos+2,4)) & ";" & Mid(sStr,iPos+6)  
      iPos = InStr(1,sStr,"\u") 
   Wend
   ConvertUnicodeEntities = sStr
End Function

Function GetJSONField(sJSON As String, sField As String) As String
   iLoc = InStr(1,sJSON, Chr(34) & sField & Chr(34))
   sStr = sJSON
   If iLoc > 0 Then
      sStr = Mid(sStr,iLoc+Len(sField)+3)
      sFirstChar = Left(sStr,1)
      sStr = Mid(sStr,2)
      If sFirstChar = Chr(34) Then
         sStr = Left(sStr,InStr(1,sStr,Chr(34))-1)
      ElseIf sFirstChar = "[" Then
         sStr = Left(sStr,InStr(1,sStr,"]")-1)
      End If
      GetJSONField = sStr
   Else
      GetJSONField = ""
   End If
End Function

Sub GetLanguageList(ByRef aLangTable$())
   Dim oHTTP As Object
   Set oHTTP = CreateObject("WinHttp.WinHttpRequest.5.1")
   
   sURL = sScriptShifterURL & sLangEndpoint
   
   oHTTP.Open "GET", sURL, False
   oHTTP.SetRequestHeader "Content-Type", "application/json"
   oHTTP.Send

   sResponse = oHTTP.ResponseText
   
   iNextPos = InStr(1,sResponse,"},")
   iCount = 0
   While iNextPos > 0 
      iCount = iCount+1
      iNextPos = InStr(iNextPos+1,sResponse,"},")
   Wend
   
   ReDim aLangList(iCount)
   ReDim aLangTable(iCount,4)
   
   iCount = 0
   Do 
      sCurrentLang = sResponse
      If iNextPos > 0 Then
         sCurrentLang = Left(sResponse,iNextPos)
         sResponse = Mid(sResponse,iNextPos+1)
      End If
      sCurrentLang = Mid(sCurrentLang, InStr(1,sCurrentLang,Chr(34))+1)
      sDetails = Mid(sCurrentLang, InStr(1, sCurrentLang, "{"))
      sCurrentLang = Left(sCurrentLang, InStr(1,sCurrentLang, Chr(34))-1)
      
      sLabel = sDetails
      sLabel = Mid(sLabel,InStr(1,sLabel,"""label"":""")+9)
      sLabel = Left(sLabel,InStr(1,sLabel,Chr(34))-1)
      
      sMARC = sDetails
      sMARC = Mid(sMARC,InStr(1,sMARC,"""marc_code"":""")+13)
      sMARC = Left(sMARC,InStr(1,sMARC,Chr(34))-1)


      sStoR =  sDetails
      sStoR = Mid(sStoR,InStr(sStoR,"""has_s2r"":")+10)
      sStoR = Left(sStoR,InStr(sStoR,",")-1)
     
      sRtoS =  sDetails
      sRtoS = Mid(sRtoS,InStr(sRtoS,"""has_r2s"":")+10)
      sRtoS = Left(sRtoS,InStr(sRtoS,",")-1)
       
      sDirectionality = "both"
      If sStoR = "true" And sRtoS = "false" Then
         sDirectionality = "s2r"
      ElseIf sStoR = "false" And sRtoS = "true" Then
         sDirectionality = "r2s"
      End If

      
      aLangList(iCount) = sLabel
      aLangTable(iCount,0) = sLabel
      aLangTable(iCount,1) = sCurrentLang
      aLangTable(iCount,2) = sMARC
      aLangTable(iCount,3) = sDirectionality
      iCount = iCount + 1
      iNextPos = InStr(1,sResponse,"},")
   Loop While iNextPos > 0
End Sub

Function IsRomanized(sString As String) As Integer
   IsRomanized = True
   iPos = 1
   While iPos > 0 
      iPos = iPos + 1
      iPos = InStr(iPos,sString,"\u")
      If iPos > 0 Then
         sCode = Mid(sString,iPos+2,4)
         If (sCode >= "0370" And sCode <= "FE1F") Or (sCode >= "FE30") Then
            IsRomanized = False
         End If
      End If  
   Wend
End Function


Sub SetExclusions(sExclusionList As String)
   iNextComma = InStr(1,sExclusionList,",")   
   While iNextComma > 0  
      sNextExclusion = Left(sExclusionList,iNextComma-1)      
      AddExclusion(sNextExclusion)
      sExclusionList = Mid(sExclusionList,iNextComma+1)
      iNextComma = InStr(1,sExclusionList,",")  
   Wend
   If sExclusionList <> "" Then
      AddExclusion(sExclusionList)
   End If
End Sub

Function AddExclusion(sSubfield as String)
   sSubfield = ReplaceAll(sSubfield,"'","")
   Dim aCurrentExclusions()
   ReDim aCurrentExclusions(UBound(aExcludeSubfields$))
   For i = 0 to UBound(aExcludeSubfields$)
      If sSubfield = aExcludeSubfields(i) Then
         MsgBox("Exlcusion already exists")
         Exit Function
      End If
      aCurrentExclusions(i) = aExcludeSubfields(i)
   Next i
   
   ReDim aExcludeSubfields(UBound(aExcludeSubfields$)+1)
   For i = 0 to UBound(aCurrentExclusions)
      aExcludeSubfields(i) = aCurrentExclusions(i)
   Next i   
   aExcludeSubfields(UBound(aExcludeSubfields$)-1) = sSubfield
End Function

Function IsExcluded(sSubfield As String) As Integer
   If Len(sSubfield) = 3 Then
      sSubfield = sSubfield & "a"
   End If
   For i = 0 to UBound(aExcludeSubfields$)
      bExcluded = True
      sExclude_i = aExcludeSubfields(i)
      For j = 1 to 4
         If (Mid(sExclude_i,j,1) <> Mid(sSubField,j,1) And (j = 4 Or Mid(sExclude_i,j,1) <> "x")) Then
            bExcluded = False
         End If
      Next j
      If bExcluded Then
         IsExcluded = True
         Exit Function
      End If
   Next i
   IsExcluded = False
End Function

Function GetUnicodeSubstring(sStr As String, iStart As Integer, iLength As Integer) As String
   iStrLen = Len(sStr)
   iActualPos = 1
   iActualLen = 0
   sSubstring = ""
   sChar = ""
   bInEntity = False
   For i = 1 to iStrLen
      If Mid(sStr,i,3) = "&#x" Then
         bInEntity = True
      ElseIf Mid(sStr,i,1) = ";" Then
         bInEntity = False
      End If
      If iActualPos >= iStart And iActualLen < iLength Then
         sSubstring = sSubstring & Mid(sStr,i,1)
         If Not bInEntity Then
            iActualLen = iActualLen + 1
         End If
      End If
      If Not bInEntity Then
         iActualPos = iActualPos + 1
      End If
   Next i
   GetUnicodeSubstring = sSubstring
End Function
 
Function GetCurrentField(CS As Object) As String

   'Insert BOM at beginning of field to force all Unicode to appear as numeric entities
   CS.EndCell
   CS.InsertText("&#xFEFF;")
   
   bSuccess = CS.GetFieldLineUnicode(CS.CursorRow,sFieldX)
   CS.EndCell
   CS.SendKeys "",-1 
   CS.SendKeys "{BS}",-1
   
   'Remove BOM
   sFieldX = ReplaceAll(sFieldX, "&#xFEFF;","")
   
   CS.CopyField   
   sFieldY = Clipboard.GetText
      
   sFieldZ = ""
   
   y = 1
   For x = 1 to Len(sFieldX)
      If i <= Len(sFieldX) - 7 And Mid(sFieldX,x,3) = "&#x" Then
         sFieldZ = sFieldZ & Mid(sFieldX,x,8) 
         x = x + 7
      Else
         If Mid(sFieldX,x,1) <> Mid(sFieldY,y,1) And Mid(sFieldY,y,1) <> "?" Then
            sFieldZ = sFieldZ & Mid(sFieldY,y,1)
         Else
            sFieldZ = sFieldZ & Mid(sFieldX,x,1)
         End If
      End If
      y = y + 1
   Next x
   GetCurrentField = sFieldZ
End Function
